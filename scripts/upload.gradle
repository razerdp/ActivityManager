apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

def siteUrl = 'https://github.com/razerdp/ActivityManager'
def gitUrl = 'https://github.com/razerdp/ActivityManager.git'


group = rootProject.ext.group
version = rootProject.ext.versionName


install {
    repositories.mavenInstaller {
        pom.artifactId = ARTIFACT_ID
        pom {
            project {
                packaging PKG
                name ARTIFACT_ID
                description 'Activity管理库'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'razerdp'
                        name 'razerdp'
                        email 'razerdp123@gmail.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }

                if (IGNORE_DEPENDENCE) {
                    pom.withXml {
                        asNode().dependencies.'*'.each {
                            it.delete
                        }
                    }
                }
            }
        }
    }
}

if (project.getPlugins().hasPlugin('com.android.application') ||
        project.getPlugins().hasPlugin('com.android.library')) {
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }
    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        failOnError false
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
} else {
    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "https://developer.android.google.cn/reference/"
        addStringOption('Xdoclint:none', '-quiet')
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publish = true
    override = true

    pkg {
        repo = "maven"
        name = ARTIFACT_ID
        userOrg = 'razerdp'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = siteUrl + '/issues'
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        publish = true
    }
}



